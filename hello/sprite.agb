INCLUDE "gbhw.inc"

_SY EQU _OAMRAM
_SX EQU _OAMRAM + 1
_SN EQU _OAMRAM + 2
_SA EQU _OAMRAM + 3

_MX EQU _RAM
_MY EQU _RAM + 1

SECTION "hello_sprite", ROM0[$0100]

  nop
  jp setup

  ROM_HEADER ROM_NOMBC, ROM_SIZE_32KBYTE, RAM_SIZE_0KBYTE

setup:

  nop
  di
  ld sp, $FFFF

begin:

  ld a, %11100100
  ld [rBGP], a
  ld [rOBP0], a

  ld a, 0
  ld [rSCX], a
  ld [rSCY], a

  call turn_off_lcd

load_tiles:

  ld hl, tiles
  ld de, _VRAM
  ld b, 32

load_tiles_loop:

  ld a, [hl]
  ld [de], a
  dec b
  jr z, clear_background
  inc hl
  inc de
  jr load_tiles_loop

clear_background:

  ld hl, _SCRN0
  ld de, 32 * 32

clear_background_loop:

  ld a, 0
  ld [hl], a
  dec de
  ld a, d
  or e
  jp z, clear_sprites
  inc hl
  jp clear_background_loop

clear_sprites:

  ld      hl, _OAMRAM
  ld      de, 40 * 4

clear_sprites_loop:

  ld      a, 0
  ld      [hl], a
  dec     de
  ld      a, d
  or      e
  jp      z, prepare_sprite
  inc     hl
  jp      clear_sprites_loop

prepare_sprite:

  ld a, 30
  ld [_SY], a
  ld [_SX], a

  ld a, 1
  ld [_SN], a
  ld [_MX], a
  ld [_MY], a

  ld a, 0
  ld [_SA], a

  ld a, LCDCF_ON|LCDCF_BG8000|LCDCF_BG9800|LCDCF_BGON|LCDCF_OBJ8|LCDCF_OBJON
  ld [rLCDC], a

animate:

wait:

  ld a, [rLY]
  cp 145
  jp nz, wait

y_adjustment:

  ld a, [_SY]
  ld hl, _MY
  add a, [hl]
  ld hl, _SY
  ld [hl], a
  cp 152
  jp z, go_upward
  cp 16
  jp z, go_downward
  jp x_adjustment

go_upward:

  ld a, -1
  ld [_MY], a
  jp x_adjustment

go_downward:
  
  ld a, 1
  ld [_MY], a

x_adjustment:

  ld a, [_SX]
  ld hl, _MX
  add a, [hl]
  ld hl, _SX
  ld [hl], a
  cp 160
  jp z, go_left
  cp 8
  jp z, go_right
  jp slow_down

go_left:

  ld a, -1
  ld [_MX], a
  jp slow_down

go_right:
  ld a, 1
  ld [_MX], a

slow_down:

  call go_slower
  jp animate

go_slower:

  ld de, 2000

delay:

  dec de
  ld a, d
  or e
  jp z, end_delay
  nop
  jp delay

end_delay:

  ret

turn_off_lcd:

  ld a, [rLCDC]
  rlca
  ret nc 

wait_for_vblank:

  ld a, [rLY]
  cp 145
  jr nz, wait_for_vblank
  ld a, [rLCDC]
  res 7, a
  ld [rLCDC], a
  ret

tiles:
  DB  $AA, $00, $44, $00, $AA, $00, $11, $00
  DB  $AA, $00, $44, $00, $AA, $00, $11, $00
  DB  $3E, $3E, $41, $7F, $41, $6B, $41, $7F
  DB  $41, $63, $41, $7F, $3E, $3E, $00, $00
end_tiles:
